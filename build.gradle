plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
}

group 'com.lifeinide'
version '1.0.0'
description 'Common reusable frames for data filtering from REST/GraphQL.'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

publishing {
    def token = project.findProperty("token")
    if (!token) {
        try {
            token = new File(System.getProperty("user.home"), ".github-token").text
        } catch (e) {
            logger.warn("Please put personal github token into ~/.github-token file or pass it with -Ptoken=??? parameter.")
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/l0co/rest-data-filter")
            credentials {
                username = project.findProperty("user") ?: "l0co"
                password = token
            }
        }
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: property('vSlf4j')

    compileOnly group: 'javax.persistence', name:'javax.persistence-api', version: property('vJpa')
    compileOnly group: 'org.hibernate', name: 'hibernate-core', version: property('vHibernate')
    compileOnly group: 'org.hibernate', name: 'hibernate-search-engine', version: property('vHibernateSearch')
    compileOnly group: 'org.hibernate', name: 'hibernate-search-orm', version: property('vHibernateSearch')
    compileOnly group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.0.7.RELEASE'
    compileOnly group: 'org.springframework.data', name: 'spring-data-mongodb', version: '2.0.7.RELEASE'

    testCompile group: 'javax.persistence', name:'javax.persistence-api', version: property('vJpa')
    testCompile group: 'org.hibernate', name: 'hibernate-core', version: property('vHibernate')
    testCompile group: 'com.h2database', name: 'h2', version: property('vH2')
    testCompile group: 'org.hibernate', name: 'hibernate-search-engine', version: property('vHibernateSearch')
    testCompile group: 'org.hibernate', name: 'hibernate-search-orm', version: property('vHibernateSearch')
    testCompile group: 'commons-io', name: 'commons-io', version: property('vCommonsIo')

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: property('vJunit')
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: property('vJunit')
    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: property('vLogback')
}

def installer = install.repositories.mavenInstaller

// alter maven pom
installer.pom.whenConfigured {pom ->
    pom.project {
        name project.name
        description description
        url 'https://github.com/l0co/rest-data-filter'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        // remove test dependencies
        for (Iterator itr = pom.dependencies.iterator(); itr.hasNext(); )
            if (itr.next().scope == 'test')
                itr.remove()
    }
}


